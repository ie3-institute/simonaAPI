plugins {
  id 'groovy' // groovy support
  id 'java' // java support
  id 'com.diffplug.spotless' version '6.23.3'//code format
  id 'pmd' // code check, working on source code
  id 'com.github.spotbugs' version '6.0.5' // code check, working on byte code
  id "org.sonarqube" version "4.4.1.3373" // sonarqube
  id 'signing'
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id 'jacoco' // java code coverage plugin
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
}

ext {
  javaVersion = JavaVersion.VERSION_17
  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins

  // required for pekko
  scalaVersion = "2.13"
  scalaBinaryVersion = "2.13.12"
  pekkoVersion = "1.0.2"
}

group = 'com.github.ie3-institute'
description = 'simonaAPI'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'jacoco.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'test.gradle'

repositories {
  mavenCentral()
}

dependencies{

  implementation 'tech.units:indriya:2.2' // quantities

  // scala (needed for pekko)
  implementation "org.scala-lang:scala-library:${scalaBinaryVersion}"

  // pekko
  implementation "org.apache.pekko:pekko-actor_${scalaVersion}:${pekkoVersion}"
  testImplementation "org.apache.pekko:pekko-testkit_${scalaVersion}:${pekkoVersion}" // pekko testkit

  // TESTING
  testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

task printVersion {
  doLast {
    println project.version
  }
}
