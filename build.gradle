plugins {
  id 'groovy' // groovy support
  id 'java' // java support
  id 'com.diffplug.spotless' version '7.2.1'//code format
  id 'pmd' // code check, working on source code
  id 'com.github.spotbugs' version '6.3.0' // code check, working on byte code
  id "org.sonarqube" version "6.3.1.5724" // sonarqube
  id 'signing'
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id 'jacoco' // java code coverage plugin
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
  id 'kr.motd.sphinx' version '2.10.1' // documentation generation
}

ext {
  javaVersion = JavaVersion.VERSION_21
  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins

  // required for pekko
  scalaVersion = '3'
  scalaBinaryVersion = '3.7.3'
  pekkoVersion = "1.2.0"
}

group = 'com.github.ie3-institute'
description = 'simonaAPI'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'jacoco.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'test.gradle'
apply from: scriptsLocation + 'branchName.gradle'

repositories {
  mavenCentral()
}

dependencies{

  implementation 'tech.units:indriya:2.2.3' // quantities

  // scala (needed for pekko)
  implementation "org.scala-lang:scala3-library_${scalaVersion}:${scalaBinaryVersion}"

  //PSU
  implementation('com.github.ie3-institute:PowerSystemUtils:3.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }

  //PSDM
  implementation('com.github.ie3-institute:PowerSystemDataModel:8.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }

  // logging
  implementation platform('org.apache.logging.log4j:log4j-bom:2.25.1')
  implementation 'org.apache.logging.log4j:log4j-api' // log4j
  implementation 'org.apache.logging.log4j:log4j-core' // log4j
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

  // pekko
  implementation "org.apache.pekko:pekko-actor-typed_${scalaVersion}:${pekkoVersion}"

  testImplementation "org.apache.pekko:pekko-actor-testkit-typed_${scalaVersion}:${pekkoVersion}" // pekko typed testkit

  // TESTING
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.apache.groovy:groovy:4.0.28'
  testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

tasks.register('printVersion') {
  doLast {
    println project.version
  }
}
