plugins {
  id 'groovy' // groovy support
  id 'java' // java support
  id 'com.diffplug.spotless' version '6.4.2'//code format
  id 'pmd' // code check, working on source code
  id 'com.github.spotbugs' version '5.0.6' // code check, working on byte code
  id "org.sonarqube" version "3.3" // sonarqube
  id 'signing'
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id 'jacoco' // java code coverage plugin
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
}

ext {
  javaVersion = JavaVersion.VERSION_17
  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins

  // required for akka
  scalaVersion = "2.13"
}

group = 'com.github.ie3-institute'
description = 'simonaAPI'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'jacoco.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'test.gradle'

repositories {
  mavenCentral()
  maven { url "https://www.jitpack.io" } // allows github repos as dependencies
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies{

  implementation 'tech.units:indriya:2.1.3' // quantities
  implementation 'com.github.ie3-institute:PowerSystemDataModel:3.0-SNAPSHOT'
  implementation 'com.github.ie3-institute:PowerSystemUtils:2.0-SNAPSHOT'

  // akka
  implementation platform("com.typesafe.akka:akka-bom_${scalaVersion}:2.6.19")
  implementation "com.typesafe.akka:akka-actor_${scalaVersion}"
  testImplementation "com.typesafe.akka:akka-testkit_${scalaVersion}"

  // TESTING
  testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
  testImplementation 'org.mockito:mockito-core:4.4.0'

}

task printVersion {
  doLast {
    println project.version
  }
}
